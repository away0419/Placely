// Auth 모듈 - 인증/인가 담당

// Kotlin 플러그인과 kapt 플러그인 추가 (Q클래스 생성용)
plugins {
    id 'org.jetbrains.kotlin.kapt'
}

dependencies {
	// 공통 모듈
	implementation project(':common')
	
	// Spring Boot 핵심
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	
	// 데이터베이스
	implementation 'com.oracle.database.jdbc:ojdbc11:23.3.0.23.09'
	implementation 'com.oracle.database.jdbc:ucp:23.3.0.23.09'
	
	// Redis (토큰 관리용)
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Google Simple JSON
	implementation 'com.googlecode.json-simple:json-simple:1.1.1'

	// QueryDSL (Kotlin 설정)
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta' // Jakarta EE 지원
	kapt 'com.querydsl:querydsl-apt:5.0.0:jakarta' // Kotlin용 annotation processor

	// API 문서화 - springdoc-openapi
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	
	// 테스트 의존성
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.h2database:h2'
	
	// Kotlin 테스트 라이브러리
	testImplementation 'io.kotest:kotest-runner-junit5:5.8.0'
	testImplementation 'io.kotest:kotest-assertions-core:5.8.0'
	testImplementation 'io.kotest.extensions:kotest-extensions-spring:1.1.2'
	testImplementation 'io.mockk:mockk:1.13.8'
	testImplementation 'com.ninja-squad:springmockk:4.0.2'
}

// QueryDSL Q클래스 생성 설정 (Kotlin용)
kapt {
    arguments {
        arg("querydsl.entityAccessors", "true") // 엔티티 접근자 생성
        arg("querydsl.useFields", "false") // 필드 대신 메서드 사용
    }
    generateStubs = true // 스텁 생성
}

// 생성된 Q클래스 경로 설정
def generatedSourcesDir = layout.buildDirectory.dir("generated/source/kapt/main").get().asFile

sourceSets {
	main {
		kotlin {
			srcDirs += [generatedSourcesDir]
		}
	}
}